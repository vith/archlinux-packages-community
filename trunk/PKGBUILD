# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Rabyte <rabyte [at] gmail (dot) com>

pkgname=assaultcube
pkgver=1.2.0.2
pkgrel=5
pkgdesc='A realistic team oriented multiplayer FPS based on the Cube engine'
arch=('x86_64')
url='http://assault.cubers.net/'
license=('ZLIB' 'custom')
depends=('sdl' 'sdl_mixer' 'sdl_image' 'openal' 'zlib' 'gcc-libs' 'libgl' 'glu' 'curl')
makedepends=('mesa' 'clang')
source=("https://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.tar.bz2"
        "https://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.source.tar.bz2"
        assaultcube-gcc6.patch
        'assaultcube'
        'assaultcube-server'
        'assaultcube.desktop'
        'assaultcube.png')
sha512sums=('9563970403578c946ce03561621e2ea203604e3c3d9d1d784b62122a7f65eaa2e238ac4b88052336a0f18171da0e94342bf3d73f732bceb86eb58a6111e70d7b'
            '4f7748e9d4c2496de6f82552af3e1a9fe764a3fcb045e0b92653dc359e070bf78349a7763c3293ee9aea4afb112a0f303768e85ed6346262366030dedd2a64ed'
            '94cf3a73340dbdaefceaeefe83f2ca39635b80eb11cd01c48581c258e5b1dc55f0736ddc9cde4c1a3856bf5649a9fbaa5da417e15fc8f83ec1d65771fc71bf5d'
            '0f2e8d21b54132e8bd37dc45be9aa4660b25185fbde9b4cc6ea21b1ac75dfefb5e6667a8169e252df473f86a5bd8246f08b32a64f022d5c4c5d9f2aa6fbc0d74'
            '20e712142f89f4c57f4e1424410d941ed6196c2904f68f04a86c7c4cdd425d541e44b216fd9dff20a6d4738fab0a2c7ed67fd59e63280cd7a007c3873d58086c'
            '208a62d7c2cf0522920a7982b1513a4b0c786b24a7693ca1d924b8ce60aa505c1d48ddb81325a4e64530170a268c6161e71c40476b6385453a952b6fe8b0a84c'
            '08f27ad80380eda0e83746c0a5689a792bafd2d215213b7486bb52a43612c41d2f3ab0d84a8bb0c37247ff9a6739663a7f485a7f47026d3bbf47bd50b90225b3')

prepare() {
  cd AssaultCube_v${pkgver}.source
  # Fix build with GCC 6
  patch -p1 -i ../assaultcube-gcc6.patch
}

build() {
  cd AssaultCube_v${pkgver}.source/source/src

  make
}

package() {
  cd AssaultCube_v${pkgver}.source/source/src
  install -Dm755 ac_client "${pkgdir}"/usr/bin/ac_client
  install -Dm755 ac_server "${pkgdir}"/usr/bin/ac_server

  cd "${srcdir}"/AssaultCube_v${pkgver}
  mkdir -p "${pkgdir}"/usr/share/assaultcube
  cp -rf config packages docs mods "${pkgdir}"/usr/share/assaultcube
  install -Dm644 "${srcdir}"/assaultcube.png "${pkgdir}"/usr/share/pixmaps/assaultcube.png
  install -Dm644 "${srcdir}"/assaultcube.desktop "${pkgdir}"/usr/share/applications/assaultcube.desktop
  install -Dm644 docs/package_copyrights.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 "${srcdir}"/assaultcube "${pkgdir}"/usr/bin/assaultcube
  install -Dm755 "${srcdir}"/assaultcube-server "${pkgdir}"/usr/bin/assaultcube-server
}

# vim: sw=2:ts=2 et:
